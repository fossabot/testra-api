buildscript {
    ext {
        kotlinVersion = '1.2.50'
        springBootVersion = '2.0.0.RELEASE'
        js2pPluginVersion = '0.4.33'
        dependencyManagementVersion = '1.0.5.RELEASE'
        kotlinLinterVersion = '1.14.0'
        gradleDockerVersion = '1.2'
        dependencyCheckVersion = '3.2.1'
    }
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:$dependencyManagementVersion"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jsonschema2pojo:jsonschema2pojo-gradle-plugin:$js2pPluginVersion"
        classpath "gradle.plugin.org.jmailen.gradle:kotlinter-gradle:$kotlinLinterVersion"
        classpath "se.transmode.gradle:gradle-docker:$gradleDockerVersion"
        classpath "org.owasp:dependency-check-gradle:$dependencyCheckVersion"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC7-3"
    id "com.github.ben-manes.versions" version "0.20.0"
}

apply plugin: 'idea'

allprojects {
    group 'tech.testra'
    version '1.0-SNAPSHOT'

    apply plugin: 'project-report'

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'kotlin-spring'
    apply plugin: 'org.jmailen.kotlinter'
    apply plugin: 'org.owasp.dependencycheck'
    apply plugin: 'io.gitlab.arturbosch.detekt'

    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

    compileKotlin {
        kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-starter-parent:$springBootVersion"
        }
        dependencies {
            dependency 'org.modelmapper:modelmapper:2.0.0'
            dependency "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
            dependency 'org.apache.commons:commons-lang3:3.0'
            dependency 'io.mockk:mockk:1.8.5'
            dependency 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.6'
        }
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.8.1'
}