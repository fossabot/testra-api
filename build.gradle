buildscript {
    ext {
        kotlinVersion = '1.2.31'
        springBootVersion = '2.0.0.RELEASE'
        js2pPluginVersion = '0.4.33'
        dependencyManagementVersion = '1.0.5.RELEASE'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:${dependencyManagementVersion}"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "org.jsonschema2pojo:jsonschema2pojo-gradle-plugin:${js2pPluginVersion}"
        classpath "gradle.plugin.org.jmailen.gradle:kotlinter-gradle:1.11.1"
        classpath 'se.transmode.gradle:gradle-docker:1.2'
        classpath 'org.owasp:dependency-check-gradle:3.1.2'
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC6-3"
}

apply plugin: 'idea'

allprojects {
    group 'tech.testra'
    version '1.0-SNAPSHOT'

    apply plugin: 'project-report'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'kotlin-spring'
    apply plugin: 'org.jmailen.kotlinter'
    apply plugin: 'org.owasp.dependencycheck'
    apply plugin: 'io.gitlab.arturbosch.detekt'

    sourceCompatibility = targetCompatibility = 1.8

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-starter-parent:$springBootVersion"
        }
        dependencies {
            dependency 'org.modelmapper:modelmapper:1.1.0'
            dependency "org.jetbrains.kotlin:kotlin-stdlib-jre8:${kotlinVersion}"
            dependency 'org.apache.commons:commons-lang3:3.0'
            dependency 'io.mockk:mockk:1.7.15'
        }
    }
}