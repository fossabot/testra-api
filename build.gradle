buildscript {
    ext {
        kotlinVersion = '1.3.20'
        springBootVersion = '2.1.2.RELEASE'
        js2pPluginVersion = '1.0.0'
        dependencyManagementVersion = '1.0.6.RELEASE'
        gradleDockerVersion = '1.2'
        dependencyCheckVersion = '4.0.2'
    }
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:$dependencyManagementVersion"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jsonschema2pojo:jsonschema2pojo-gradle-plugin:$js2pPluginVersion"
        classpath "se.transmode.gradle:gradle-docker:$gradleDockerVersion"
        classpath "org.owasp:dependency-check-gradle:$dependencyCheckVersion"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC7-3"
    id "com.github.ben-manes.versions" version "0.20.0"
    id "org.jmailen.kotlinter" version "1.21.0"
}

apply plugin: 'idea'

allprojects {
    group 'tech.testra'
    version '0.4-SNAPSHOT'

    apply plugin: 'project-report'

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'kotlin-spring'
    apply plugin: 'org.jmailen.kotlinter'
    apply plugin: 'org.owasp.dependencycheck'
    apply plugin: 'io.gitlab.arturbosch.detekt'

    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

    compileKotlin {
        kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-starter-parent:$springBootVersion"
        }
        dependencies {
            dependency 'org.modelmapper:modelmapper:2.3.2'
            dependency "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
            dependency 'org.apache.commons:commons-lang3:3.8.1'
            dependency 'io.mockk:mockk:1.9'
            dependency 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.8'
            dependency 'org.apache.commons:commons-csv:1.6'
        }
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
        }
    }
}

task gradleWrapper(type: Wrapper) {
    gradleVersion = '5.1.1'
}